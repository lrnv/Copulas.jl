#Using the convention that generator satisfies Ï•(0) = 1 (this is opposite to e.g. https://en.wikipedia.org/wiki/Copula_(probability_theory))


# We should follow closely
# https://www.uni-ulm.de/fileadmin/website_uni_ulm/mawi.inst.zawa/forschung/preprintmariushofert.pdf
# for the implementation of sampling methods. 

"""
    WilliamsonGenerator{d,TÏ•,TX}

Fields:
    - Ï•::TÏ• -- a function representing the archimedean generator.
    - X::TX -- a random variable that represents its williamson d-transform

Constructors

    WilliamsonGenerator(X::Distributions.UnivariateDistribution, d)

The WilliamsonCopula is the barebone Archimedean Copula that directly leverages the Williamson transform and inverse transform (in their greatest generalities), that are implemented in [WilliamsonTransforms.jl](https://www.github.com/lrnv/WilliamsonTransforms.jl). You can construct it by providing the Williamson-d-tranform as a (non-negative) random variable `X::Distributions.UnivariateDistribution`, or by providing the ``d``-monotone generator `Ï•::Function` as a function from ``\\mathbb R_+`` to ``[0,1]``, decreasing and d-monotone. The other component will be computed automatically. You also have the option to provide both, which will probably be faster if you have an analytical expression for both. 

About `WilliamsonCopula(X::Distributions.UnivariateDistribution, d)`: For a univariate non-negative random variable ``X``, with cumulative distribution function ``F`` and an integer ``d\\ge 2``, the Williamson-d-transform of ``X`` is the real function supported on ``[0,\\infty[`` given by:

```math
\\phi(t) = ð’²_{d}(X)(t) = \\int_{t}^{\\infty} \\left(1 - \\frac{t}{x}\\right)^{d-1} dF(x) = \\mathbb E\\left( (1 - \\frac{t}{X})^{d-1}_+\\right) \\mathbb 1_{t > 0} + \\left(1 - F(0)\\right)\\mathbb 1_{t <0}
```

This function has several properties: 
- We have that ``\\phi(0) = 1`` and ``\\phi(Inf) = 0``
- ``\\phi`` is ``d-2`` times derivable, and the signs of its derivatives alternates : ``\\forall k \\in 0,...,d-2, (-1)^k \\phi^{(k)} \\ge 0``.
- ``\\phi^{(d-2)}`` is convex.

These properties makes this function what is called an *archimedean generator*, able to generate *archimedean copulas* in dimensions up to ``d``. 

About `WilliamsonCopula(Ï•::Function, d)`: On the other hand, `WilliamsonCopula(Ï•::Function, d)` Computes the inverse Williamson d-transform of the d-monotone archimedean generator Ï•. 

A ``d``-monotone archimedean generator is a function ``\\phi`` on ``\\mathbb R_+`` that has these three properties:
- ``\\phi(0) = 1`` and ``\\phi(Inf) = 0``
- ``\\phi`` is ``d-2`` times derivable, and the signs of its derivatives alternates : ``\\forall k \\in 0,...,d-2, (-1)^k \\phi^{(k)} \\ge 0``.
- ``\\phi^{(d-2)}`` is convex.

For such a function ``\\phi``, the inverse Williamson-d-transform of ``\\phi`` is the cumulative distribution function ``F`` of a non-negative random variable ``X``, defined by : 

```math
F(x) = ð’²_{d}^{-1}(\\phi)(x) = 1 - \\frac{(-x)^{d-1} \\phi_+^{(d-1)}(x)}{k!} - \\sum_{k=0}^{d-2} \\frac{(-x)^k \\phi^{(k)}(x)}{k!}
```

We return this cumulative distribution function in the form of the corresponding random variable `<:Distributions.ContinuousUnivariateDistribution` from `Distributions.jl`. You may then compute : 
    - The cdf via `Distributions.cdf`
    - The pdf via `Distributions.pdf` and the logpdf via `Distributions.logpdf`
    - Samples from the distribution via `rand(X,n)`


References: 
    Williamson, R. E. (1956). Multiply monotone functions and their Laplace transforms. Duke Math. J. 23 189â€“207. MR0077581
    McNeil, Alexander J., and Johanna NeÅ¡lehovÃ¡. "Multivariate Archimedean copulas, d-monotone functions and â„“ 1-norm symmetric distributions." (2009): 3059-3097.
"""




"""
    ArchimedeanCopula{d, TG}

Fields: 
    - G::TG : the generator <: Generator. 

Constructor: 

    ArchimedeanCopula(d::Int,G::Generator)

For some archimedean generator `G::Generator` and some dimenson `d`, this class models the archimedean copula wich has this generator. More formally, a ``d``-monotone archimedean generator is a function ``\\phi`` on ``\\mathbb R_+`` that has these three properties:
- ``\\phi(0) = 1`` and ``\\phi(Inf) = 0``
- ``\\phi`` is ``d-2`` times derivable, and the signs of its derivatives alternates : ``\\forall k \\in 0,...,d-2, (-1)^k \\phi^{(k)} \\ge 0``.
- ``\\phi^{(d-2)}`` is convex.

In the code, this function is implemented as `Ï•(G::Generator,t)`, and its max monotony `d` is given by `max_monotony(G)`. The corresponding d-variate archimedean copula writes: 

```math
C(\\mathbf u) = \\phi^{-1}\\left(\\sum_{i=1}^d \\phi(u_i)\\right)
```

It can be sampled through A Radial simplex decomposition as follows: If ``\\mathbf U \\sim C``, then ``U \\equal R \\mathbf S`` for a random vector ``\\mathbf S \\sim`` `Dirichlet(ones(d))`, that is uniformity on the d-variate simplex, and a non-negative random variable ``R`` that is the Williamson d-transform of `\\phi`. The density, cdf, kendall tau, and may other properties of the Archimedean copula are then directly deduced from this relationship. 

There exists several known parametric generators that are implement in the package. For every `NamedGenerator <: Generator` implemented in the package, we provide a type alias ``NamedCopula{d,...} = ArchimedeanCopula{d,NamedGenerator{...}}` to be able to manipulate the archimedean copulas without too much hassle for known and usefull special cases. 

A generic archimdeanc copula can be constructed as follows: 

```julia
struct MyGenerator <: Generator end
Ï•(G::MyGenerator,t) = exp(-t) # your archimedean generator, can be any d-monotonous function.
max_monotony(G::MyGenerator) = Inf # could depend on generators parameters. 
C = ArchimedeanCopula(d,MyGenerator())
```

The obtained model can be used as follows: 
```julia
spl = rand(C,1000)   # sampling
cdf(C,spl)           # cdf
pdf(C,spl)           # pdf
loglikelihood(C,spl) # llh
```

The following functions have defaults but can be overridden for performance: 

```julia
Ï•â»Â¹(C::MyArchimedean, t)        # Inverse of Ï•
Ï•â½Â¹â¾(C::MyArchimedean, t)       # first defrivative of Ï•
Ï•â½áµˆâ¾(C::MyArchimedean,t)        # dth defrivative of Ï•
Ï„(C::MyArchimedean)             # Kendall tau
Ï„â»Â¹(::Type{MyArchimedean},Ï„) =  # Inverse kendall tau
fit(::Type{MyArchimedean},data) # fitting.
```

As a bonus, If you know the williamson d-transform of your generator and not your generator itself, you may take a look at [`WilliamsonGenerator`](@ref) that implements them. If you rather know the frailty distribution, take a look at `WilliamsonFromFrailty`.
"""
struct ArchimedeanCopula{d,TG} <: Copula{d}
    G::TG
    function ArchimedeanCopula(d::Int,G::Generator)
        @assert d <= max_monotony(G) "The generator you provided is not d-monotonous according to its max_monotonicity property, and thus this copula does not exists."
        return new{d,typeof(G)}(G)
    end
end
Ï•(C::ArchimedeanCopula{d,TG},t)    where {d,TG} = Ï•(C.G,t)
Ï•â»Â¹(C::ArchimedeanCopula{d,TG},t)  where {d,TG} = Ï•â»Â¹(C.G,t)
Ï•â½Â¹â¾(C::ArchimedeanCopula{d,TG},t) where {d,TG} = Ï•â½Â¹â¾(C.G,t)
Ï•â½áµâ¾(C::ArchimedeanCopula{d,TG},k,t) where {d,TG} = Ï•â½áµâ¾(C.G,k,t)
williamson_dist(C::ArchimedeanCopula{d,TG}) where {d,TG} = williamson_dist(C.G,d)


function _cdf(C::CT,u) where {CT<:ArchimedeanCopula} 
    sum_Ï•â»Â¹u = 0.0
    for us in u
        sum_Ï•â»Â¹u += Ï•â»Â¹(C,us)
    end
    return Ï•(C,sum_Ï•â»Â¹u)
end
function Distributions._logpdf(C::ArchimedeanCopula{d,TG}, u) where {d,TG}
    if !all(0 .<= u .<= 1)
        return eltype(u)(-Inf)
    end
    sum_Ï•â»Â¹u = 0.0
    sum_logÏ•â½Â¹â¾Ï•â»Â¹u = 0.0
    for us in u
        Ï•â»Â¹u = Ï•â»Â¹(C,us)
        sum_Ï•â»Â¹u += Ï•â»Â¹u
        sum_logÏ•â½Â¹â¾Ï•â»Â¹u += log(-Ï•â½Â¹â¾(C,Ï•â»Â¹u)) # log of negative here because Ï•â½Â¹â¾ is necessarily negative
    end
    numer = Ï•â½áµâ¾(C, d, sum_Ï•â»Â¹u)
    dimension_sign = iseven(d) ? 1.0 : -1.0 #need this for log since (-1.0)áµˆ Ï•â½áµˆâ¾ â‰¥ 0.0


    # I am not sure this is the right reasoning :
    if numer == 0
        if sum_logÏ•â½Â¹â¾Ï•â»Â¹u == -Inf
            return Inf
        else
            return -Inf
        end
    else
        return log(dimension_sign*numer) - sum_logÏ•â½Â¹â¾Ï•â»Â¹u
    end
end

# function Ï„(C::ArchimedeanCopula)  
#     return 4*Distributions.expectation(r -> Ï•(C,r), williamson_dist(C)) - 1
# end

function _archi_rand!(rng,C::ArchimedeanCopula{d},R,x) where d
    # x is assumed to already be random exponentials produced by Random.randexp
    r = rand(rng,R)
    sx = sum(x)
    for i in 1:d
        x[i] = Ï•(C,r * x[i]/sx)
    end
end

function Distributions._rand!(rng::Distributions.AbstractRNG, C::CT, x::AbstractVector{T}) where {T<:Real, CT<:ArchimedeanCopula}
    # By default, we use the williamson sampling. 
    Random.randexp!(rng,x)
    _archi_rand!(rng,C,williamson_dist(C),x)
    return x
end
function Distributions._rand!(rng::Distributions.AbstractRNG, C::CT, A::DenseMatrix{T}) where {T<:Real, CT<:ArchimedeanCopula}
    # More efficient version that precomputes the williamson transform on each call to sample in batches: 
    Random.randexp!(rng,A)
    R = williamson_dist(C)
    for i in 1:size(A,2)
        _archi_rand!(rng,C,R,view(A,:,i))
    end
    return A
end
function Distributions.fit(::Type{CT},u) where {CT <: ArchimedeanCopula}
    # @info "Archimedean fits are by default through inverse kendall tau."
    d = size(u,1)
    Ï„ = StatsBase.corkendall(u')
    # Then the off-diagonal elements of the matrix should be averaged: 
    avgÏ„ = (sum(Ï„) .- d) / (d^2-d)
    Î¸ = Ï„â»Â¹(CT,avgÏ„)
    return CT(d,Î¸)
end

Ï„(C::ArchimedeanCopula{d,TG}) where {d,TG} = Ï„(C.G)
function Ï„â»Â¹(::Type{T},Ï„_val) where {T<:ArchimedeanCopula}
    return Ï„â»Â¹(generatorof(T),Ï„_val)
end


################################################################################################
################                                                                ################
################ Define all "named" archimedean copulas at once like that :     ################
################                                                                ################
################################################################################################

################################################################################################
# Deal with easy cases with meta-programming: 
const AMHCopula{d,T} = ArchimedeanCopula{d,AMHGenerator{T}}
AMHCopula(d,Î¸) = ArchimedeanCopula(d,AMHGenerator(Î¸))
generatorof(::Type{AMHCopula}) = AMHGenerator

const ClaytonCopula{d,T} = ArchimedeanCopula{d,ClaytonGenerator{T}}
ClaytonCopula(d,Î¸) = ArchimedeanCopula(d,ClaytonGenerator(Î¸))
generatorof(::Type{ClaytonCopula}) = ClaytonGenerator

const FrankCopula{d,T} = ArchimedeanCopula{d,FrankGenerator{T}}
FrankCopula(d,Î¸) = ArchimedeanCopula(d,FrankGenerator(Î¸))
generatorof(::Type{FrankCopula}) = FrankGenerator

const GumbelBarnettCopula{d,T} = ArchimedeanCopula{d,GumbelBarnettGenerator{T}}
GumbelBarnettCopula(d,Î¸) = ArchimedeanCopula(d,GumbelBarnettGenerator(Î¸))
generatorof(::Type{GumbelBarnettCopula}) = GumbelBarnettGenerator

const GumbelCopula{d,T} = ArchimedeanCopula{d,GumbelGenerator{T}}
GumbelCopula(d,Î¸) = ArchimedeanCopula(d,GumbelGenerator(Î¸))
generatorof(::Type{GumbelCopula}) = GumbelGenerator

const InvGaussianCopula{d,T} = ArchimedeanCopula{d,InvGaussianGenerator{T}}
InvGaussianCopula(d,Î¸) = ArchimedeanCopula(d,InvGaussianGenerator(Î¸))
generatorof(::Type{InvGaussianCopula}) = InvGaussianGenerator

const JoeCopula{d,T} = ArchimedeanCopula{d,JoeGenerator{T}}
JoeCopula(d,Î¸) = ArchimedeanCopula(d,JoeGenerator(Î¸))
generatorof(::Type{JoeCopula}) = JoeGenerator

const IndependentCopula{d} = ArchimedeanCopula{d,IndependentGenerator}
IndependentCopula(d) = ArchimedeanCopula(d,IndependentGenerator())
generatorof(::Type{IndependentCopula}) = IndependentGenerator

const MCopula{d} = ArchimedeanCopula{d,MGenerator}
MCopula(d) = ArchimedeanCopula(d,MGenerator())
generatorof(::Type{MCopula}) = MGenerator

const WCopula{d} = ArchimedeanCopula{d,WGenerator}
WCopula(d) = ArchimedeanCopula(d,WGenerator())
generatorof(::Type{WCopula}) = WGenerator

# The zero-variate ones just need a few more methods: 
Distributions._logpdf(::ArchimedeanCopula{d,IndependentGenerator}, u) where {d} = all(0 .<= u .<= 1) ? zero(eltype(u)) : eltype(u)(-Inf)
Distributions._logpdf(::ArchimedeanCopula{d,MGenerator},           u) where {d} = all(u == u[1]) ?     zero(eltype(u)) : eltype(u)(-Inf)
Distributions._logpdf(::ArchimedeanCopula{d,WGenerator},           u) where {d} = sum(u) == 1 ?   zero(eltype(u)) : eltype(u)(-Inf)

_cdf(::ArchimedeanCopula{d,IndependentGenerator}, u) where d = prod(u)
_cdf(::ArchimedeanCopula{d,MGenerator},           u) where {d} = minimum(u)
_cdf(::ArchimedeanCopula{d,WGenerator},           u) where {d} = max(1 + sum(u)-d,0)

function Distributions._rand!(rng::Distributions.AbstractRNG, ::ArchimedeanCopula{d,IndependentGenerator}, x::AbstractVector{T}) where {d,T<:Real}
    Random.rand!(rng,x)
end
function Distributions._rand!(rng::Distributions.AbstractRNG, ::ArchimedeanCopula{d,MGenerator}, x::AbstractVector{T}) where {d,T<:Real}
    x .= rand(rng)
end
function Distributions._rand!(rng::Distributions.AbstractRNG, ::ArchimedeanCopula{d,WGenerator}, x::AbstractVector{T}) where {d,T<:Real}
    @assert d==2
    x[1] = rand(rng)
    x[2] = 1-x[1] 
end


function Distributions._rand!(rng::Distributions.AbstractRNG, C::ArchimedeanCopula{d,IndependentGenerator}, A::DenseMatrix{T}) where {T<:Real, d}
    Random.rand!(rng,A)
    return A
end
function Distributions._rand!(rng::Distributions.AbstractRNG, C::ArchimedeanCopula{d,MGenerator}, A::DenseMatrix{T}) where {T<:Real, d}
    A[1,:] .= rand(rng,size(A,2))
    for i in 2:size(A,1)
        A[i,:] .= A[1,:]
    end
    return A
end
function Distributions._rand!(rng::Distributions.AbstractRNG, C::ArchimedeanCopula{d,WGenerator}, A::DenseMatrix{T}) where {T<:Real, d}
    @assert size(A,1) == 2
    A[1,:] .= rand(rng,size(A,2))
    A[2,:] .= 1 .- A[1,:]
    return A
end

